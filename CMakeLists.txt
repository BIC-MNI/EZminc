# CMakeFiles.txt for the EZminc
#
# Vladimir S. FONOV - vladimir.fonov@gmail.com

PROJECT(ezminc)
CMAKE_MINIMUM_REQUIRED(VERSION 3.6)

set(CMAKE_CXX_STANDARD 14)
set(CMAKE_CXX_STANDARD_REQUIRED ON)


SET(BICPL_FOUND OFF)

IF(NOT MINC_TOOLKIT_BUILD)
  SET(CMAKE_MODULE_PATH ${CMAKE_MODULE_PATH} "${CMAKE_CURRENT_SOURCE_DIR}/cmake-modules")

  FIND_PACKAGE(LIBMINC REQUIRED)
  FIND_PACKAGE(GSL)
  FIND_PACKAGE(FFTW3F)
  FIND_PACKAGE(BICPL QUIET)

  FIND_PACKAGE(ITK REQUIRED)
  LINK_DIRECTORIES(${ITK_LIBRARY_DIRS})
  INCLUDE( ${LIBMINC_USE_FILE} )

  OPTION(EZMINC_BUILD_MINCNLM  "Build Non-local means denoising programm,requires GSL (protected by a license IDDN.FR.001.070033.000.S.P.2007.000.21000)" OFF)
  OPTION(EZMINC_BUILD_DISTORTION_CORRECTION "Build distortion correction tools, requires GSL" OFF)

  IF(HAVE_MINC1) # Right now it is needed for C++ simple minc interface
    OPTION(EZMINC_BUILD_MRFSEG   "Build mrfseg - tissue classification program from Jussi Tohka" OFF)
  ENDIF(HAVE_MINC1)

  OPTION(EZMINC_BUILD_DD    "Build Diffeomorphic Demons (requires ITK)" OFF)
  OPTION(EZMINC_BUILD_TOOLS "Build additional tools , requires GSL" OFF)

  IF(ITK_FOUND)
    IF( ITK_VERSION_MAJOR VERSION_GREATER_EQUAL 4 )
      # Assume that ITK4 and later has built-in MINC support
      SET(MINC4ITK_LIBRARY_DIRS )
      SET(MINC4ITK_INCLUDE_DIRS )
      SET(HAVE_MINC4ITK OFF)
    ELSE()
      FIND_PACKAGE(MINC4ITK REQUIRED)
      SET(HAVE_MINC4ITK ON)
    ENDIF()
  ENDIF(ITK_FOUND)
ELSE(NOT MINC_TOOLKIT_BUILD)
  SET(BICPL_FOUND ON) # Assume that minc-toolkit will provide BICPL
  SET(GSL_FOUND ON) # Assume minc-toolkit has GSL pre-compiled
ENDIF(NOT MINC_TOOLKIT_BUILD)

# have to be in front of ITK includes to avoid using wrong dwt.h header
INCLUDE_DIRECTORIES( ${CMAKE_CURRENT_SOURCE_DIR}/image_proc ${CMAKE_CURRENT_SOURCE_DIR}/tools)


IF(ITK_FOUND)
  INCLUDE(${ITK_USE_FILE})
  LINK_DIRECTORIES(${ITK_LIBRARY_DIRS} )
  INCLUDE_DIRECTORIES(${ITK_INCLUDE_DIRS})  
ENDIF(ITK_FOUND)

IF(HAVE_MINC4ITK)
  SET(ITK_LIBRARIES ITKIOMINC ${ITK_LIBRARIES})
ENDIF(HAVE_MINC4ITK)

INCLUDE(CheckIncludeFiles)
#INCLUDE(CheckFunctionExists)
INCLUDE(CheckSymbolExists)

#CHECK_FUNCTION_EXISTS(finite    HAVE_FINITE)
#CHECK_FUNCTION_EXISTS(isfinite  HAVE_ISFINITE)
CHECK_SYMBOL_EXISTS(finite    "math.h" HAVE_FINITE)
CHECK_SYMBOL_EXISTS(isfinite  "math.h" HAVE_ISFINITE)

CHECK_INCLUDE_FILES(values.h    HAVE_VALUES_H)


CONFIGURE_FILE(${CMAKE_CURRENT_SOURCE_DIR}/config.h.cmake ${CMAKE_CURRENT_BINARY_DIR}/config.h @ONLY)
INCLUDE_DIRECTORIES( ${CMAKE_CURRENT_BINARY_DIR} )
ADD_DEFINITIONS(-DHAVE_CONFIG_H)
ADD_SUBDIRECTORY( image_proc )

ADD_SUBDIRECTORY( scripts )

IF(GSL_FOUND)
  INCLUDE_DIRECTORIES( ${GSL_INCLUDE_DIR})
  IF(EZMINC_BUILD_MINCNLM)
    ADD_SUBDIRECTORY(mincnlm)
  ENDIF(EZMINC_BUILD_MINCNLM)
ENDIF(GSL_FOUND)

IF(FFTW3F_FOUND)
  INCLUDE_DIRECTORIES( ${FFTW3F_INCLUDE_DIRS})
ENDIF(FFTW3F_FOUND)

IF(EZMINC_BUILD_TOOLS )
  ADD_SUBDIRECTORY(tools)
ENDIF(EZMINC_BUILD_TOOLS)

IF(GSL_FOUND AND EZMINC_BUILD_DISTORTION_CORRECTION)
  ADD_SUBDIRECTORY( distortion_correction )
ENDIF(GSL_FOUND AND EZMINC_BUILD_DISTORTION_CORRECTION)

IF(EZMINC_BUILD_MRFSEG AND HAVE_MINC1)
  ADD_SUBDIRECTORY(mrfseg)
ENDIF(EZMINC_BUILD_MRFSEG AND HAVE_MINC1)

IF(ITK_FOUND)
  IF(EZMINC_BUILD_DD)
    ADD_SUBDIRECTORY(diff_demons)
  ENDIF(EZMINC_BUILD_DD)
ENDIF(ITK_FOUND)

ADD_SUBDIRECTORY(examples)
